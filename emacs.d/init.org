#+title: My Emacs Config
#+property: header-args :tangle init.el

* My Emacs Config

Here is the literate version of my Emacs configuration. Everything is contained in a single org-mode file which can be tangled with =org-babel= to produce an Emacs Lisp init file.

#+BEGIN_SRC emacs-lisp
;;;;
;;
;; This file was tangled from `init.org' using Org Babel -- see that
;; file for a literate version of this configuration.
;;
;;;;
#+END_SRC


** Startup
*** Slim down the GUI

#+BEGIN_SRC emacs-lisp
(when (fboundp 'menu-bar-mode) (menu-bar-mode -1))
(when (fboundp 'tool-bar-mode) (tool-bar-mode -1))
(when (fboundp 'scroll-bar-mode) (scroll-bar-mode -1))
#+END_SRC

*** Start with a blank \*scratch\* buffer

#+BEGIN_SRC emacs-lisp
(setq inhibit-startup-screen t)
(setq initial-scratch-message nil)
#+END_SRC

*** Customize
Keep customized settings in a separate file. Load them here to set up fonts early.

#+BEGIN_SRC emacs-lisp
(setq custom-file "~/.emacs.d/custom.el")
(load custom-file 'noerror)
#+END_SRC
** General Settings
*** This is not the bell you are looking for

#+BEGIN_SRC emacs-lisp
(setq ring-bell-function 'ignore)
#+END_SRC

*** There can be only one (file encoding)

To really force UTF-8, you will need to customize =file-coding-alist=.

#+BEGIN_SRC emacs-lisp
(prefer-coding-system 'utf-8)
(set-default-coding-systems 'utf-8)
#+END_SRC

*** Backups? We don’t need no stinkin’ backups!

#+BEGIN_SRC emacs-lisp
(setq auto-save-default nil)
(setq make-backup-files nil)
#+END_SRC

*** They’re against my religion

#+BEGIN_SRC emacs-lisp
(setq-default indent-tabs-mode nil)
(setq-default tab-width 4)
#+END_SRC

*** In (white)space, no one can see you (unless you turn on =whitespace-mode=)

[[http://ergoemacs.org/emacs/whitespace-mode.html][Simplify]] the display of whitespace characters in =whitespace-mode=, and show trailing whitespace in programming modes.

#+BEGIN_SRC emacs-lisp
(setq whitespace-style '(spaces tabs newline space-mark tab-mark newline-mark))

(setq whitespace-display-mappings
      '(
        (space-mark 32 [183] [46]) ; 32 SPACE, 183 MIDDLE DOT 「·」, 46 FULL STOP 「.」
        (newline-mark 10 [182 10]) ; 10 LINE FEED
        (tab-mark 9 [9655 9] [92 9]) ; 9 TAB, 9655 WHITE RIGHT-POINTING TRIANGLE 「▷」
        ))

(add-hook 'prog-mode-hook
          (lambda ()
            (setq show-trailing-whitespace t)))
#+END_SRC
*** I don’t need your help (I have the [f1] key)

#+BEGIN_SRC emacs-lisp
(define-key key-translation-map [?\C-h] [?\C-?])
#+END_SRC

*** Beautiful snowflakes

#+BEGIN_SRC emacs-lisp
(require 'uniquify)
(setq uniquify-buffer-name-style 'forward)
#+END_SRC

*** Sentences end with a single space
#+begin_src emacs-lisp
(setq sentence-end-double-space nil)
#+end_src

*** Electrify me

#+BEGIN_SRC emacs-lisp
(electric-indent-mode 1)
#+END_SRC

*** SHOW ME (the matching parens)

#+BEGIN_SRC emacs-lisp
(add-hook 'prog-mode-hook
          (lambda ()
            (show-paren-mode)))
#+END_SRC
** OS-Specific Settings and Directories

On Windows, emacs (or the =runemacs.exe= binary) starts up in the folder it lives in, and the =HOME= environment variable is set to the =%APPDATA%/Roaming= folder. This is rather inconvenient, so the first thing to do is to create a new shortcut to =runemacs.exe= named ‘emacs’, and set the “Start In:” property to, say, =C:\\Users\\<username>=. This shortcut can live anywhere; I have it pinned to my taskbar. The =HOME= variable we leave alone, since it is used by Emacs to find the =.emacs.d= directory. We don’t need it anyway. For convenience we define some useful directories as constants.

*** Testing OS type

#+BEGIN_SRC emacs-lisp
(defun windows-p ()
  (eq system-type 'windows-nt))
#+END_SRC

*** Path-munging functions

#+BEGIN_SRC emacs-lisp
(defun add-to-path-string (dir path)
  (if (string-equal "" (or path ""))
      dir
    (concat dir path-separator path)))

(defun add-to-exec-path (dir)
  (setenv "PATH"
          (add-to-path-string
           (expand-file-name dir "~")
           (getenv "PATH")))
  (add-to-list 'exec-path dir))
#+END_SRC

*** Linux-only

On Linux, I keep some useful scripts and executables in =~/.local/bin/=.

#+BEGIN_SRC emacs-lisp
(unless (windows-p)
  (add-to-exec-path (expand-file-name ".local/bin" "~")))
#+END_SRC

*** Define directory constants

#+BEGIN_SRC emacs-lisp
(defconst dir/emacsd (file-name-as-directory
                      (expand-file-name ".emacs.d" (getenv "HOME")))
  "Emacs configuration directory.")

(defconst dir/home (file-name-as-directory
                    (if (windows-p)
                        (getenv "USERPROFILE")
                      (expand-file-name "~")))
  "User's home directory.")

(defconst dir/org (file-name-as-directory
                   (expand-file-name
                    (if (windows-p)
                        "Documents/Org"
                      "Org")
                    dir/home))
  "Org file directory.")
#+END_SRC

** Useful stuff
*** Keybindings

There are a lot of things that are useful to toggle on and off; various minor modes such as =whitespace-mode=, line truncation, etc. We define a common keymap to use for these commands:
#+BEGIN_SRC emacs-lisp
(setq toggle/prefix "C-x t")
(setq toggle/keymap (make-sparse-keymap))

(defun toggle/bind-key (key command)
  (define-key toggle/keymap key command))

(global-set-key (kbd toggle/prefix) toggle/keymap)
#+END_SRC

Go ahead and define some toggle commands for built in stuff:
#+BEGIN_SRC emacs-lisp
(toggle/bind-key "l" 'whitespace-mode)
(toggle/bind-key "w" 'toggle-truncate-lines)
(toggle/bind-key "e" 'electric-indent-mode)
#+END_SRC

*** Winner mode

Winner mode saves and restores window configurations. Enable it.
#+BEGIN_SRC emacs-lisp
(winner-mode 1)
#+END_SRC

** Packages
*** Initialization

We need to initialize the package system and manually install and load [[https://github.com/jwiegley/use-package][use-package]] so that we can use it to manage other packages.

Load =package.el=:
#+BEGIN_SRC emacs-lisp
(require 'package)
#+END_SRC

Add the MELPA and Org-mode repositories:
#+BEGIN_SRC emacs-lisp
(add-to-list 'package-archives
             '("melpa" . "http://melpa.milkbox.net/packages/") t)
(add-to-list 'package-archives
             '("org" . "http://orgmode.org/elpa/") t)
#+END_SRC
The Org-mode ELPA repository can be used to install the latest released version of org-mode, but it must be installed manually to override the built-in version.

Initialize the package system and download a list of packages if it doesn’t exist (i.e. on a new system or it has been removed manually):
#+BEGIN_SRC emacs-lisp
(package-initialize)

(unless package-archive-contents
  (package-refresh-contents))
#+END_SRC

Now install and require use-package:
#+BEGIN_SRC emacs-lisp
(unless (package-installed-p 'use-package)
  (package-install 'use-package))

(require 'use-package)
#+END_SRC

And now we are ready to use =use-package= to configure our packages, and it will even download them for us!
*** Evil

Load evil first because we will want to configure powerline and key bindings for some later packages:

#+BEGIN_SRC emacs-lisp
(use-package evil
  :ensure t
  :demand t
  :init
  (progn
    ; from https://github.com/syl20bnr/spacemacs/blob/master/spacemacs/packages.el
    (setq evil-mode-line-format 'before)
    (setq evil-emacs-state-cursor  '("red" box))
    (setq evil-normal-state-cursor '("orange" box))
    (setq evil-visual-state-cursor '("black" box))
    (setq evil-insert-state-cursor '("green3" bar))
    (setq evil-motion-state-cursor '("purple" box))
    )
  :config
  (progn

    ; load evil-leader before (evil-mode 1)
    (use-package evil-leader
      :ensure t
      :init
      (progn
        (evil-leader/set-leader ",")
        (global-evil-leader-mode))
      :config
      (progn
        (evil-leader/set-key "t" toggle/keymap)
        ))

    (evil-mode 1)

    (use-package evil-surround
      :ensure t
      :init (global-evil-surround-mode 1))

    ;; add useful keys
    (define-key evil-normal-state-map (kbd "C-w q") 'evil-quit)
    (define-key evil-insert-state-map (kbd "C-g") 'evil-normal-state)

    ;; unbind keys
    ;; insert
    (define-key evil-insert-state-map (kbd "C-e") nil)
    (define-key evil-insert-state-map (kbd "C-p") nil)
    (define-key evil-insert-state-map (kbd "C-n") nil)
    ;; normal
    (define-key evil-normal-state-map (kbd "C-p") nil)
    (define-key evil-normal-state-map (kbd "C-n") nil)
    (define-key evil-normal-state-map (kbd "M-.") nil)
    ))
#+END_SRC

*** Theme and Powerline

The powerline config is mostly from: https://github.com/syl20bnr/spacemacs/blob/master/spacemacs/packages.el.
#+BEGIN_SRC emacs-lisp
(use-package powerline
  :ensure t
  :demand t
  :config
  (progn
    (toggle/bind-key "m" 'powerline-minor-modes-toggle)

    (defun propertize-evil-mode-line-tag ()
      (propertize evil-mode-line-tag 'font-lock-face
                  ;; Don't propertize if we're not in the selected buffer
                  (cond ((not (eq (current-buffer) (car (buffer-list)))) '())
                        ((evil-insert-state-p) '(:background "green3" :foreground "black"))
                        ((evil-emacs-state-p)  '(:background "red" :foreground "black"))
                        ((evil-motion-state-p) '(:background "purple" :foreground "black"))
                        ((evil-visual-state-p) '(:background "gray" :foreground "black"))
                        ((evil-normal-state-p)  '(:background "orange" :foreground "black"))
                        (t '()))))

    (defpowerline powerline-evil-mode (propertize-evil-mode-line-tag))

    (defvar powerline-minor-modes-p t)
    (defun powerline-minor-modes-toggle ()
      "Toggle display of minor modes."
      (interactive)
      (if powerline-minor-modes-p
          (setq powerline-minor-modes-p nil)
        (setq powerline-minor-modes-p t)))

    (defun my-powerline-theme ()
      "Set up my powerline theme with evil mode etc."
      (interactive)
      (setq-default mode-line-format
                    '("%e"
                      (:eval
                       (let* ((active (eq (frame-selected-window) (selected-window)))
                              (face1 (if active 'powerline-active1 'powerline-inactive1))
                              (face2 (if active 'powerline-active2 'powerline-inactive2))
                              (lhs (append (list
                                            ;; (powerline-window-number face1 'l)
                                            (powerline-evil-mode face1 'l)

                                            (powerline-raw "%*" nil 'l)
                                            (powerline-buffer-size nil 'l)
                                            (powerline-buffer-id nil 'l)
                                            (powerline-raw " " nil)

                                            (powerline-arrow-right nil face1)
                                            (powerline-major-mode face1 'l)
                                            (powerline-raw " " face1))

                                           (if powerline-minor-modes-p
                                               (list (powerline-arrow-right face1 nil)
                                                     (powerline-minor-modes nil 'l)
                                                     (powerline-raw mode-line-process nil 'l)
                                                     (powerline-raw " " nil)
                                                     (powerline-arrow-right nil face2))
                                             (list (powerline-raw " " face1)
                                                   (powerline-arrow-right face1 face2)))

                                           (list (powerline-vc face2))))
                              (rhs (list
                                    (powerline-raw global-mode-string face2 'r)
                                    (powerline-raw " " face2)

                                    (powerline-arrow-left face2 face1)
                                    (powerline-raw " " face1)
                                    (powerline-raw "%l:%2c" face1 'r)
                                    (powerline-arrow-left face1 nil)
                                    (powerline-raw " " nil)
                                    (powerline-raw "%p" nil 'r)

                                    (powerline-hud face2 face1))))
                         (concat
                          (powerline-render lhs)
                          (powerline-fill face2 (powerline-width rhs))
                          (powerline-render rhs)))))))

    ;; setting the powerline mode line in 'after-init-hook solves problems with other packages screwing it up (looking at you, workgroups2!)
    (add-hook 'after-init-hook (lambda () (my-powerline-theme)))
    ))
#+END_SRC

Load a theme:
#+BEGIN_SRC emacs-lisp
(use-package moe-theme
  :ensure t
  :config
  (progn
    ;; Resize titles
    ;; (setq moe-theme-resize-markdown-title '(2.0 1.7 1.5 1.3 1.0 1.0))
    ;; (setq moe-theme-resize-org-title '(2.2 1.8 1.6 1.4 1.2 1.0 1.0 1.0 1.0))
    ;; (setq moe-theme-resize-rst-title '(2.0 1.7 1.5 1.3 1.1 1.0))

    (setq moe-theme-mode-line-color 'green)

    (moe-dark)
    (powerline-moe-theme)
    ))
#+END_SRC

#+BEGIN_SRC emacs-lisp :tangle no
(use-package ample-theme
  :ensure t
  :config (load-theme 'ample t))
#+END_SRC

#+BEGIN_SRC emacs-lisp :tangle no
(use-package zenburn-theme
  :ensure t
  :config
  (progn (load-theme 'zenburn t)))
#+END_SRC

*** Org Mode

#+BEGIN_SRC emacs-lisp
(use-package org
  :defer t
  :commands org-agenda
  :init
  (progn
    ;; general org-mode behavior
    (setq org-directory dir/org
          org-startup-indented t
          org-startup-folded 'content
          org-use-speed-commands t
          org-catch-invisible-edits 'error
          )

    ;; org-agenda
    (define-key global-map (kbd "C-c a") 'org-agenda)
    (setq org-agenda-files (list (expand-file-name "agenda.org" dir/org))
          org-agenda-span 'month
          org-agenda-window-setup 'current-window
          org-log-done t
          )

    ;; enable org-export backends
    (setq org-export-backends '(ascii html latex beamer))
    )
  :config
  (progn

    ;; configure sub packages and other packages used with org-mode
    (use-package org-indent :diminish "")
    (use-package htmlize :ensure t)


    ;; do more intensive customization of org export:
    ;; - latex
    (setq org-latex-pdf-process '("latexmk -g -xelatex %f")
          org-latex-default-packages-alist nil
          org-latex-packages-alist '(("" "graphicx" nil) ("" "float" nil))
          org-latex-default-class "default"
          )
    (setq org-latex-classes ; this really calls for a macro
          '(
            ("default"
             "\\documentclass[11pt]{article}
[PACKAGES]
\\usepackage{amssymb}
\\usepackage{fontspec}
\\usepackage{xunicode}
\\usepackage{url}
\\usepackage{hyperref}"
             ("\\section{%s}" . "\\section*{%s}")
             ("\\subsection{%s}" . "\\subsection*{%s}")
             ("\\subsubsection{%s}" . "\\subsubsection*{%s}")
             ("\\paragraph{%s}" . "\\paragraph*{%s}")
             ("\\subparagraph{%s}" . "\\subparagraph*{%s}"))

            ("beamer"
             "\\documentclass{beamer}
[PACKAGES]
\\usepackage{amssymb}
\\usepackage{fontspec}
\\usepackage{xunicode}
\\usepackage{url}
\\usepackage{hyperref}
\\setbeamertemplate{navigation symbols}{}
"
             ("\\section{%s}" . "\\section*{%s}")
             ("\\subsection{%s}" . "\\subsection*{%s}")
             ("\\subsubsection{%s}" . "\\subsubsection*{%s}")
             ("\\paragraph{%s}" . "\\paragraph*{%s}")
             ("\\subparagraph{%s}" . "\\subparagraph*{%s}"))
            ))


    ;; org-babel for literate programming
    (org-babel-do-load-languages
     'org-babel-load-languages
     '(
       (clojure . t)
       (emacs-lisp . t)
       (gnuplot . t)
       (python . t)
       (sh . t)
      )
    )
    (setq org-src-fontify-natively t)
    (setq org-src-preserve-indentation t)
    ))
#+END_SRC

**** TODO split into sub-blocks using noweb

**** TODO create macro to make configuring latex classes and packages easier and more flexible

**** TODO improve org-babel configuration

*** Magit

#+BEGIN_SRC emacs-lisp
(use-package magit
  :ensure t
  :commands magit-status
  :diminish magit-auto-revert-mode
  :bind ("C-x g" . magit-status)
  :config
  (define-key magit-status-mode-map (kbd "k") nil)
  (define-key magit-status-mode-map (kbd "K") 'magit-discard-item)
  )
#+END_SRC

*** Helm

See helm configs at [[https://github.com/bbatsov/prelude][Prelude]] and [[http://tuhdo.github.io/helm-intro.html][A Package in a league of its own: Helm]]
.
#+begin_src emacs-lisp
(use-package helm
  :ensure t
  :diminish ""
  :init
  (progn

    (setq helm-command-prefix-key "C-c h")

    (require 'helm-config)
    (require 'helm-eshell)
    (require 'helm-files)
    (require 'helm-grep)

    (define-key helm-map (kbd "<tab>") 'helm-execute-persistent-action)
    (define-key helm-map (kbd "C-i") 'helm-execute-persistent-action)
    (define-key helm-map (kbd "C-z")  'helm-select-action)

    (setq helm-candidate-number-limit 10)

    (setq helm-idle-delay 0.1
          helm-input-idle-delay 0.01
          helm-quick-update t
          helm-M-x-requires-pattern nil
          helm-ff-skip-boring-files t
          helm-buffers-fuzzy-matching t
          helm-ff-file-name-history-use-recentf t
          helm-move-to-line-cycle-in-source t
          helm-split-window-default-side 'other
          helm-split-window-in-side-p t
          )

    (evil-leader/set-key ":" 'helm-M-x
                         "b" 'helm-mini)


    (global-set-key (kbd "C-c h o") 'helm-occur)
    (global-set-key (kbd "C-c h g") 'helm-do-grep)

    (helm-mode)

    (use-package helm-gtags
      :ensure t
      :defer t
      :diminish ""
      :init
      (progn
        (setq helm-gtags-prefix-key "\C-cg"
              helm-gtags-suggested-key-mapping nil
              helm-gtags-ignore-case t
              helm-gtags-auto-update t
              helm-gtags-use-input-at-cursor t
              helm-gtags-pulse-at-cursor t
              )

        (add-hook 'dired-mode-hook 'helm-gtags-mode)
        (add-hook 'eshell-mode-hook 'helm-gtags-mode)
        (add-hook 'prog-mode-hook 'helm-gtags-mode)
        )
      :config
      (progn
        (global-set-key (kbd "M-.") 'helm-gtags-dwim)
        ;; (define-key helm-gtags-mode-map (kbd "M-,") 'helm-gtags-pop-stack)
        ;; (define-key helm-gtags-mode-map (kbd "C-c <") 'helm-gtags-previous-history)
        ;; (define-key helm-gtags-mode-map (kbd "C-c >") 'helm-gtags-next-history)
        ))
    ))
#+end_src

*** Completion

#+BEGIN_SRC emacs-lisp
(use-package company
  :ensure t
  :diminish ""
  :config
  (progn

    (use-package semantic
      :diminish abbrev-mode
      :disabled t
      :config
      (progn
        (global-semanticdb-minor-mode 1)
        (global-semantic-idle-scheduler-mode 1)
        (global-semantic-stickyfunc-mode 1)

        (semantic-mode 1)
        ))

    (use-package company-irony
      :ensure t
      :if (not (windows-p))
      :diminish ""
      :config
      (use-package irony-mode
        :commands irony-mode
        :config
        (add-hook 'irony-mode-hook 'irony-cdb-autosetup-compile-options)
        )
      (add-to-list 'company-backends 'company-irony)
      )

    (global-company-mode)
    ))
#+END_SRC

*** TeX modes

#+BEGIN_SRC emacs-lisp
(use-package tex
  :mode ("\\.tex\\'" . latex-mode)
  :ensure auctex)
(use-package reftex
  :ensure t)
#+END_SRC

*** Programming language modes

**** Clojure

#+BEGIN_SRC emacs-lisp
(use-package clojure-mode
  :ensure t
  :mode "\\.clj\\'")

(use-package cider
  :defer t
  :ensure t)
#+END_SRC

**** Gnuplot

#+BEGIN_SRC emacs-lisp
(use-package gnuplot
  :ensure t
  :commands gnuplot-mode)
#+END_SRC

**** Julia

#+BEGIN_SRC emacs-lisp
(use-package julia-mode
  :load-path "~/.emacs.d/elisp/"
  :mode "\\.jl\\'")
#+END_SRC

**** Python

#+BEGIN_SRC emacs-lisp
(use-package python
  :commands python-mode
  :mode ("wscript\\'" . python-mode))
#+END_SRC

**** Lua

#+BEGIN_SRC emacs-lisp
(use-package lua-mode
  :ensure t
  :commands lua-mode
  :mode "\\.lua\\'")
#+END_SRC

**** C++

#+BEGIN_SRC emacs-lisp
(use-package cc-mode
  :commands (c-mode c++-mode)
  :mode ("\\.h\\(pp|h\\)?\\'" . c++-mode)
  :config

  (defun my-c-mode-common-hook ()
    ;; http://programmers.stackexchange.com/q/87250
    (font-lock-add-keywords nil
                            '(("\\<\\(assert\(.*\);\\)" 1 '(:foreground "#444444") t)
                              ("\\<assert\\(\(.*\);\\)" 1 '(:foreground "#666666") t)))
    )

  (defun my-c++-mode-hook ()
    (irony-mode)
    )
  :init
  (add-hook 'c-mode-common-hook 'my-c-mode-common-hook)
  (add-hook 'c++-mode-hook 'my-c++-mode-hook)
  )
#+END_SRC

*** Other major modes

**** Markdown

#+BEGIN_SRC emacs-lisp
(use-package markdown-mode
  :ensure t
  :commands markdown-mode)
#+END_SRC

**** Tup

#+BEGIN_SRC emacs-lisp
(use-package tup-mode
  :ensure t
  :commands tup-mode
  :mode "Tupfile.ini")
#+END_SRC

*** Unicycle mode

#+BEGIN_SRC emacs-lisp
(use-package unicycle-mode
  :load-path "~/.emacs.d/elisp/"
  :commands unicycle-mode
  :diminish (unicycle-mode . " ¶")
  :init (toggle/bind-key "u" 'unicycle-mode))
#+END_SRC

*** Undo-Tree mode

=undo-tree= is already installed and required as a dependency of some other package (org mode?), so all we do here is diminish it.

#+BEGIN_SRC emacs-lisp
(use-package undo-tree
  :diminish "")
#+END_SRC

*** Workgroups

Use workgroups2 because the original package doesn't seem to be maintained.

#+BEGIN_SRC emacs-lisp
(use-package workgroups2
  :ensure t
  :diminish (workgroups-mode . "")
  :config
  (setq wg-mode-line-display-on nil)
  (setq wg-prefix-key (kbd "C-c z"))
  (setq wg-session-file "~/.emacs.d/workgroups")
  :init
  (workgroups-mode 1)
  )
#+END_SRC
